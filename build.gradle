plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.progamers'
version = '0.0.1-SNAPSHOT'
description = 'Uno clone for Makers Engineering Project 3'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

tasks.register("listTestFiles") {
    group = "verification"
    description = "Lists all test files in the project"

    doLast {
        sourceSets.test.allSource.files
            .sort{ it.path }
            .each { file -> println "TEST FILE $file" }
    }
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}
